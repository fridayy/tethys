buildscript {
    ext {
        springBootVersion = "1.4.2.RELEASE"
        springDependencyManagementPluginVersion = "0.6.1.RELEASE"
        dockerPluginVersion = "1.2"
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("io.spring.gradle:dependency-management-plugin:$springDependencyManagementPluginVersion")
        classpath("se.transmode.gradle:gradle-docker:$dockerPluginVersion")
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "docker"

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }


    jar {
        manifest {
            attributes("Title": project.name,
                    "Version": version)
        }
    }


    configurations { provided }

    idea {
        module {
            scopes.COMPILE.plus << configurations.provided
        }
    }

    docker {
        maintainer = 'bnjm <bnjm@harmless.ninja>'
        baseImage = 'frolvlad/alpine-oraclejdk8:slim'
    }

    jar {
        manifest {
            attributes("Implementation-Title": project.name, "Implementation-Version": version)
        }
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'

    springBoot {
        bootRepackage.enabled = true
        bootRun.enabled = false
        findMainClass.enabled = true
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.RELEASE'
        }
        dependencies {
            dependency "org.spockframework:spock-core:1.1-groovy-2.4-rc-3"
            dependency "org.spockframework:spock-spring:1.1-groovy-2.4-rc-3"
        }
    }


    sourceSets {
        main {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
            runtimeClasspath += configurations.provided
        }
    }


    afterEvaluate {
        if (it.plugins.hasPlugin("groovy")) {
            sourceSets.main.java.srcDirs = []
            sourceSets.main.groovy.srcDirs += ["src/main/java"]
        }
    }
}

task wrapper(type: Wrapper) {
    String gradleVersionNumber = "3.1"
    gradleVersion = gradleVersionNumber
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersionNumber-all.zip"
}
